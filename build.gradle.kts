import net.darkhax.curseforgegradle.TaskPublishCurseForge
import org.gradle.kotlin.dsl.modCompileOnly
import org.gradle.kotlin.dsl.modLocalRuntime
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
  val kotlinVersion: String by System.getProperties()
  kotlin("jvm").version(kotlinVersion)

  id("fabric-loom") version "1.17.+"
  id("maven-publish")
  id("signing")
  id("com.modrinth.minotaur") version "2.+"
  id("net.darkhax.curseforgegradle") version "1.0.11"
}

val modVersion: String by project
val mavenGroup: String by project

val minecraftVersion: String by project
val minecraftTargetVersion: String by project
val yarnMappings: String by project
val loaderVersion: String by project
val fabricKotlinVersion: String by project
val fabricVersion: String by project

val ccVersion: String by project
val ccMcVersion: String by project
val ccTargetVersion: String by project

val configurateVersion: String by project
val clothConfigVersion: String by project
val clothApiVersion: String by project
val modMenuVersion: String by project

val trinketsVersion: String by project
val cardinalComponentsVersion: String by project

val scLibraryVersion: String by project

// ===========================
// Third party mod integration
// ===========================
val scPeripheralsVersion: String by project
val scGoodiesVersion: String by project
val figuraVersion: String by project
val common_protection_version: String by project
val ledger_version: String by project
val emi_version: String by project

val archivesBaseName = "plethora"
version = modVersion
group = mavenGroup

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(21))
  }
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType<JavaCompile>().configureEach {
  javaCompiler.set(javaToolchains.compilerFor {
    languageVersion.set(JavaLanguageVersion.of(21))
  })
  options.release.set(21)
}

tasks.withType<KotlinCompile>().configureEach {
  compilerOptions {
    jvmTarget.set(JvmTarget.JVM_21)
    apiVersion.set(KotlinVersion.KOTLIN_2_3)
    languageVersion.set(KotlinVersion.KOTLIN_2_3)
  }
}

repositories {
  mavenLocal()
  maven {
    url = uri("https://maven.reconnected.cc/releases")
    content {
      includeModule("io.sc3", "sc-library")
      includeModule("com.github.quiltservertools", "Ledger")
    }
  }

  maven {
    url = uri("https://repo.lem.sh/releases")
    content {
      includeGroup("io.sc3")
    }
  }

  maven {
    url = uri("https://maven.reconnected.cc/releases")
  }

  maven("https://maven.squiddev.cc") {
    content {
      includeGroup("cc.tweaked")
      includeModule("org.squiddev", "Cobalt")
    }
  }

  maven("https://maven.shedaniel.me") {
    // cloth-config
    content {
      includeGroup("me.shedaniel.cloth")
      includeGroup("me.shedaniel.cloth.api")
    }
  }

  maven("https://api.modrinth.com/maven") {
    content {
      includeGroup("maven.modrinth")
    }
  }

  maven("https://maven.terraformersmc.com/releases") {
    // Trinkets, mod-menu
    content {
      includeModule("dev.emi", "trinkets")
      includeModule("dev.emi", "emi-fabric")
      includeGroup("com.terraformersmc")
    }
  }

  maven("https://maven.ladysnake.org/releases") {
    // Cardinal Components API (dependency of Trinkets)
    content {
      includeGroup("dev.onyxstudios.cardinal-components-api")
      includeGroup("org.ladysnake.cardinal-components-api")
    }
  }

  maven("https://oss.sonatype.org/content/repositories/snapshots") {
    // fabric-permissions-api (dependency of sc-goodies)
    content {
      includeModule("me.lucko", "fabric-permissions-api")
    }
  }
  maven("https://maven.nucleoid.xyz/") {
    // Common Protection API
    content {
      includeGroup("eu.pb4")
      includeGroup("xyz.nucleoid")
    }
  }
  maven {
    url = uri("https://maven.figuramc.org/releases")
  }
}

dependencies {
  minecraft("com.mojang:minecraft:$minecraftVersion")
  mappings("net.fabricmc", "yarn", yarnMappings, null, "v2")
  modImplementation("net.fabricmc:fabric-loader:$loaderVersion")
  modImplementation("net.fabricmc.fabric-api:fabric-api:$fabricVersion") {
    exclude("net.fabricmc.fabric-api", "fabric-gametest-api-v1")
  }
  modImplementation("net.fabricmc:fabric-language-kotlin:$fabricKotlinVersion")

  modImplementation(include("io.sc3", "sc-library", scLibraryVersion))

  // CC: Tweaked
  modCompileOnly("cc.tweaked:cc-tweaked-$ccMcVersion-fabric:$ccVersion") {
    exclude("net.fabricmc.fabric-api", "fabric-gametest-api-v1")
  }

  modRuntimeOnly("cc.tweaked:cc-tweaked-$ccMcVersion-fabric:$ccVersion") {
    exclude("net.fabricmc.fabric-api", "fabric-gametest-api-v1")
  }


  runtimeOnly("com.electronwill.night-config:toml:3.6.5") // FIXME: CC:T has a broken night config dep

  modImplementation("dev.emi:trinkets:${trinketsVersion}")

  // Fixes @Nonnull and @Nullable annotations
  compileOnly("com.google.code.findbugs:jsr305:3.0.2")

  implementation(include("org.spongepowered", "configurate-core", configurateVersion))
  implementation(include("org.spongepowered", "configurate-hocon", configurateVersion))
  implementation(include("io.leangen.geantyref", "geantyref", "1.3.13"))
  implementation(include("com.typesafe", "config", "1.4.2"))

  modApi("maven.modrinth:cloth-config:$clothConfigVersion+fabric") {
    exclude("net.fabricmc.fabric-api")
  }
  include("maven.modrinth:cloth-config:$clothConfigVersion+fabric")
  modImplementation(include("me.shedaniel.cloth.api", "cloth-utils-v1", clothApiVersion))

  modImplementation(include("com.terraformersmc", "modmenu", modMenuVersion))

  modImplementation(include("org.ladysnake.cardinal-components-api", "cardinal-components-base", cardinalComponentsVersion))
  modImplementation(include("org.ladysnake.cardinal-components-api", "cardinal-components-entity", cardinalComponentsVersion))

  // ===========================
  // Third party mod integration
  // ===========================
  // sc-peripherals
  modCompileOnly("io.sc3:sc-peripherals:${scPeripheralsVersion}")
  // sc-goodies
  modCompileOnly("io.sc3:sc-goodies:${scGoodiesVersion}")
  // Figura
  modCompileOnly("org.figuramc:figura-fabric:${figuraVersion}") {
    exclude("net.fabricmc.fabric-api")
  }
  // Common Protection API
  modImplementation(include("eu.pb4","common-protection-api",common_protection_version))
  // Ledger
  modCompileOnly("com.github.quiltservertools","Ledger",ledger_version)

  // Fabric
  modCompileOnly("dev.emi:emi-fabric:${emi_version}:api")
  modLocalRuntime("dev.emi:emi-fabric:${emi_version}")
}

tasks {
  processResources {
    inputs.property("version", project.version)

    filesMatching("fabric.mod.json") { expand(mutableMapOf(
      "version" to project.version,
      "minecraft_target_version" to minecraftTargetVersion,
      "fabric_kotlin_version" to fabricKotlinVersion,
      "loader_version" to loaderVersion,
      "cc_target_version" to ccTargetVersion,
    )) }
  }

  jar {
    from("LICENSE") {
      rename { "${it}_${archivesBaseName}" }
    }

    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    exclude("META-INF/INDEX.LIST", "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA", "module-info.class")
  }

  remapJar {
    exclude("META-INF/INDEX.LIST", "META-INF/*.SF", "META-INF/*.DSA", "META-INF/*.RSA", "module-info.class")
    destinationDirectory.set(file("${rootDir}/build/final"))
  }

  loom {
    accessWidenerPath.set(file("src/main/resources/plethora.accesswidener"))

    sourceSets {
      main {
        resources {
          srcDir("src/generated/resources")
          exclude("src/generated/resources/.cache")
        }
      }
    }

    runs {
      configureEach {
        property("fabric.debug.disableModShuffle") // Make sure Plethora loads after CC.
      }
      create("datagen") {
        client()
        name("Data Generation")
        vmArgs(
          "-Dfabric-api.datagen",
          "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}",
          "-Dfabric-api.datagen.modid=${archivesBaseName}"
        )
        runDir("build/datagen")
      }
    }
  }
}

modrinth {
  token.set(findProperty("modrinthApiKey") as String? ?: "")
  projectId.set("LDfFdCXe")
  versionNumber.set("$minecraftVersion-$modVersion")
  versionName.set(modVersion)
  versionType.set("release")
  uploadFile.set(tasks.remapJar as Any)
  changelog.set("Release notes can be found on the [GitHub repository](https://github.com/SwitchCraftCC/Plethora-Fabric/commits/$minecraftVersion).")
  gameVersions.add(minecraftVersion)
  loaders.add("fabric")

  syncBodyFrom.set(provider { file("README.md").readText() })

  dependencies {
    required.project("fabric-api")
    required.project("fabric-language-kotlin")
    required.project("cc-tweaked")
    required.project("trinkets")
  }
}

tasks.modrinth { dependsOn(tasks.modrinthSyncBody) }
tasks.publish { dependsOn(tasks.modrinth) }

val publishCurseForge by tasks.registering(TaskPublishCurseForge::class) {
  group = PublishingPlugin.PUBLISH_TASK_GROUP
  description = "Upload artifacts to CurseForge"

  apiToken = findProperty("curseForgeApiKey") as String? ?: ""
  enabled = apiToken != ""

  val mainFile = upload("248425", tasks.remapJar.get().archiveFile)
  dependsOn(tasks.remapJar)
  mainFile.releaseType = "release"
  mainFile.changelog = "Release notes can be found on the [GitHub repository](https://github.com/SwitchCraftCC/Plethora-Fabric/commits/$minecraftVersion)."
  mainFile.changelogType = "markdown"
  mainFile.addGameVersion(minecraftVersion)
  mainFile.addRequirement("fabric-api")
  mainFile.addRequirement("fabric-language-kotlin")
  mainFile.addRequirement("cc-tweaked")
  mainFile.addRequirement("trinkets")
}

tasks.publish { dependsOn(publishCurseForge) }

publishing {
  publications {
    register("mavenJava", MavenPublication::class) {
      from(components["java"])
    }
  }

  repositories {
    maven {
      name = "reconnectedRepo"
      url = uri("https://maven.reconnected.cc/releases")

      if (!System.getenv("MAVEN_USERNAME_RCC").isNullOrEmpty()) {
        credentials {
          username = System.getenv("MAVEN_USERNAME_RCC")
          password = System.getenv("MAVEN_PASSWORD_RCC")
        }
      } else {
        credentials(PasswordCredentials::class)
      }

      authentication {
        create<BasicAuthentication>("basic")
      }
    }
  }
}

kotlin {
  // hints gradle which ide to use, including for compiling java.
  jvmToolchain(21)
}
